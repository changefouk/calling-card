buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.github.stkent.callingcard"
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 3
        versionName "1.0.0"
    }

    signingConfigs {
        release {
            String releasePropFileName = System.properties['user.home'] + "/.callingcard/callingcard.properties"
            String releaseKeyStoreFileName = System.properties['user.home'] + "/.callingcard/callingcard.jks"

            if (file(releasePropFileName).exists() && file(releaseKeyStoreFileName).exists()) {
                Properties releaseProps = new Properties()
                releaseProps.load(new FileInputStream(releasePropFileName))

                storeFile file(releaseKeyStoreFileName)
                storePassword releaseProps.password
                keyAlias releaseProps.alias
                keyPassword releaseProps.password
            }
        }
    }

    buildTypes {
        def baseAppName = "Calling Card"

        debug {
            manifestPlaceholders = [appName: "$baseAppName (Debug)"]
        }

        release {
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: baseAppName]
        }
    }
}

dependencies {
    def supportLibraryVersion = '23.4.0'
    def playServicesVersion = '9.0.2'

    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.google.android.gms:play-services-nearby:$playServicesVersion"
    compile "com.google.android.gms:play-services-auth:$playServicesVersion"

    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton:butterknife:8.0.1'

    apt 'com.jakewharton:butterknife-compiler:8.0.1'
}

apply plugin: 'com.google.gms.google-services'
